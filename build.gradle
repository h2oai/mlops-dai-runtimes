buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin',
                version: springBootPluginVersion
        classpath group: 'gradle.plugin.org.hidetake', name: 'gradle-swagger-generator-plugin',
                version: swaggerGradlePluginVersion
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: shadowJarVersion
        classpath group: 'com.diffplug.spotless', name: 'spotless-plugin-gradle', version: spotlessPluginVersion
        classpath group: 'net.ltgt.gradle', name: 'gradle-errorprone-plugin', version: errorpronePluginVersion
        classpath group: 'com.google.cloud.tools.jib', name: 'com.google.cloud.tools.jib.gradle.plugin',
                version: jibPluginVersion
        classpath group: 'org.github.ngbinh.scalastyle', name: 'gradle-scalastyle-plugin_2.11', version: '1.0.1'
    }
}

allprojects {
    // Needs explicit conversion from GString to Java String (since some plugins do not expect GString)
    version = "${swaggerApiVersion}.${minorVersion}.${patchVersion}" as String
}

subprojects {
    repositories {
        mavenCentral()
    }
}

// Collect artifacts from all the deployment templates into a single ZIP to be released.
//
// This task is to be injected content from the individual deployment templates (in their child build.gradle files)
// as follows:
//
// rootProject.distributionZip {
//   dependsOn {ARTIFACT_BUILDING_TASK}
//   into(this.project.path.replace(":", "/")) {
//        from {ARTIFACT_PATH}
//    }
// }
//
// Example for a Java zip archive:
//
// rootProject.distributionZip {
//   dependsOn buildZip
//   into(this.project.path.replace(":", "/")) {
//     from buildZip.archivePath
//   }
// }
//
task distributionZip(type: Zip) {
    archiveName "${project.name}-${project.version}.zip"
    destinationDir(file(buildDir))
}

task printVersion {
    println project.version
}
