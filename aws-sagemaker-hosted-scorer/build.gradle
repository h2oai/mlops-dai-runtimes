plugins {
    id 'org.springframework.boot'
    id 'com.google.cloud.tools.jib'
}
apply from: project(":").file('gradle/java.gradle')

dependencies {
    implementation project(':common:rest-spring-api')
    implementation project(':common:transform')
    implementation group: 'ai.h2o', name: 'mojo2-runtime-api'
    implementation group: 'ai.h2o', name: 'mojo2-runtime-impl'
    implementation group: 'io.springfox', name: 'springfox-boot-starter', version: springFoxVersion
    implementation group: 'com.google.guava', name: 'guava', version: guavaVersion
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.63'
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.0.63'
}

test {
    useJUnitPlatform()
}

bootRun {
    systemProperties System.properties
}

bootJar {
    archivesBaseName = 'serve'
}

rootProject.distributionZip {
    dependsOn bootJar
    into(this.project.path.replace(":", "/")) {
        from bootJar.archivePath
    }
}

// Docker image configuration
jib {
    from {
        image = javaBaseImage
    }
    to {
        image = dockerRepositoryPrefix + 'sagemaker-hosted-scorer'
        tags = [version]
        auth {
            username = System.getenv('TO_DOCKER_USERNAME') ?: ''
            password = System.getenv('TO_DOCKER_PASSWORD') ?: ''
        }
    }
    container {
        jvmFlags = defaultJibContainerJvmFlags.split(" ").each { it.trim() }
        ports = ['8080']
        volumes = [
                // mojo pipeline and license file will live here
                '/opt/ml/model'
        ]
        environment = [
                // location of driverless ai license file
                DRIVERLESS_AI_LICENSE_FILE: '/opt/ml/model/license.sig'
        ]
    }
}

tasks.build.dependsOn tasks.jibBuildTar
