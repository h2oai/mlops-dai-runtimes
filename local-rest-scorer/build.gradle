plugins {
    id 'com.google.cloud.tools.jib'
    id 'org.springframework.boot'
}
apply from: project(":").file('gradle/java.gradle')

dependencies {
    implementation project(':common:rest-spring-api')
    implementation project(':common:transform')
    implementation group: 'ai.h2o', name: 'h2o-genmodel'
    implementation group: 'ai.h2o', name: 'h2o-genmodel-ext-xgboost'
    implementation group: 'ai.h2o', name: 'mojo2-runtime-api'
    implementation group: 'ai.h2o', name: 'mojo2-runtime-h2o3-impl'
    implementation group: 'ai.h2o', name: 'mojo2-runtime-impl'
    constraints {
        // because https://app.snyk.io/vuln/SNYK-JAVA-COMMONSBEANUTILS-460111
        compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    }
    implementation group: 'io.springfox', name: 'springfox-boot-starter', version: springFoxVersion
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.60'

    implementation group: 'com.google.guava', name: 'guava', version: guavaVersion

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'com.google.truth.extensions', name: 'truth-java8-extension'
    testImplementation group: 'org.mockito', name: 'mockito-inline'
    testImplementation group: 'org.mockito', name : 'mockito-core'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

bootRun {
    systemProperties System.properties
}

bootJar {
    mainClass = 'ai.h2o.mojos.deploy.local.rest.ScorerApplication'
    archiveClassifier = 'boot'
}

jar {
    enabled = true
}

// Include the local rest scorer executable jar in the root package distribution zip file.
rootProject.distributionZip {
    dependsOn bootJar
    into(this.project.path.replace(":", "/")) {
        from bootJar.archivePath
    }
}

// Docker image configuration.
jib {
    from {
        image = javaBaseImage
    }
    to {
        image = dockerRepositoryPrefix + 'rest-scorer'
        tags = [version]
        auth {
            username = System.getenv('TO_DOCKER_USERNAME') ?: ''
            password = System.getenv('TO_DOCKER_PASSWORD') ?: ''
        }
    }
    container {
        jvmFlags = defaultJibContainerJvmFlags.split(" ").each { it.trim() }
        ports = ['8080']
        volumes = [
                // For storing the mojo2 file with the model to be used for scoring.
                '/mojos',
                // For the DAI license file.
                '/secrets',
        ]
        environment = [
                // The expected path to the DAI license file.
                DRIVERLESS_AI_LICENSE_FILE: '/secrets/license.sig',
        ]
    }
}

// Make docker TAR build part of the build task to ensure the image can be built.
// No pushing anywhere (not even to local docker). To push to local docker run task `jibDockerBuild` instead.
// To push to harbor use task `jib`, credentials will be needed though.
tasks.build.dependsOn tasks.jibBuildTar
