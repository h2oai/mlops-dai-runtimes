plugins {
    id 'org.springframework.boot'
    id 'org.openapi.generator'
}
apply from: project(":").file('gradle/java.gradle')

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.openapitools', name: 'jackson-databind-nullable'
}

openApiValidate {
    inputSpec = "$rootDir/common/swagger/v1openapi3/swagger.json"
    recommend = true
}

openApiGenerate {
    generatorName = 'spring'
    packageName = "ai.h2o.mojos.deploy.common.rest"
    invokerPackage = "ai.h2o.mojos.deploy.common.rest"
    inputSpec = "$rootDir/common/swagger/v1openapi3/swagger.json"
    outputDir = "$buildDir/gen"
    generateAliasAsModel = true
    globalProperties.set([
            "skipFormModel": "false",
    ])
    configOptions.set([
            "useSpringBoot3": "true",
            "interfaceOnly": "true",
            "basePackage": "ai.h2o.mojos.deploy.common.rest",
            "modelPackage": "ai.h2o.mojos.deploy.common.rest.model",
            "apiPackage": "ai.h2o.mojos.deploy.common.rest.api",
    ])
}

bootJar {
    enabled=false
}

jar {
    enabled=true
}

compileJava.dependsOn tasks.openApiValidate, tasks.openApiGenerate
sourceSets {
    main {
        java {
            srcDir("${buildDir}/gen/src/main/java")
        }
    }
}
