plugins {
    id 'org.springframework.boot'
    id 'org.openapi.generator'
}
apply from: project(":").file('gradle/java.gradle')

dependencies {
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.openapitools', name: 'jackson-databind-nullable'
}

openApiValidate {
    inputSpec = "$rootDir/common/swagger/v1/jdbc_swagger.json"
    recommend = true
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/common/swagger/v1/jdbc_swagger.json"
    outputDir = "$buildDir/gen"
    generateAliasAsModel = true
    configOptions.set([
            "useSpringBoot3": "true",
            "interfaceOnly": "true",
            "basePackage": "ai.h2o.mojos.deploy.common.rest.jdbc",
            "configPackage": "ai.h2o.mojos.deploy.common.rest.jdbc.config",
            "apiPackage": "ai.h2o.mojos.deploy.common.rest.jdbc.api",
            "modelPackage": "ai.h2o.mojos.deploy.common.rest.jdbc.model",
    ])
}

jar {
    // As a SpringBoot module, we need to enable the jar file generation.
    enabled=true
}

bootJar {
    enabled=false
}

compileJava.dependsOn tasks.openApiValidate, tasks.openApiGenerate
sourceSets {
    main {
        java {
            srcDir("$buildDir/gen/src/main/java")
        }
    }
}
