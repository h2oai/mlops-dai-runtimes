plugins {
    id 'com.google.cloud.tools.jib'
    id 'org.springframework.boot'
}
apply from: project(":").file('gradle/java.gradle')

dependencies {
    implementation project(':common:rest-vertex-ai-spring-api')
    implementation project(':common:rest-spring-api')
    implementation project(':common:transform')
    implementation group: 'ai.h2o', name: 'h2o-genmodel'
    implementation group: 'ai.h2o', name: 'h2o-genmodel-ext-xgboost'
    implementation group: 'ai.h2o', name: 'mojo2-runtime-api'
    implementation group: 'ai.h2o', name: 'mojo2-runtime-h2o3-impl'
    implementation group: 'ai.h2o', name: 'mojo2-runtime-impl'
    constraints {
        // because https://app.snyk.io/vuln/SNYK-JAVA-COMMONSBEANUTILS-460111
        compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    }
    implementation group: 'io.springfox', name: 'springfox-boot-starter', version: springFoxVersion
    implementation group: 'com.google.guava', name: 'guava', version: guavaVersion
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.slf4j', name: 'slf4j-api'
    implementation group: 'com.google.cloud', name: 'google-cloud-storage'
    implementation 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    
    
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'com.google.truth.extensions', name: 'truth-java8-extension'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

bootRun {
    systemProperties System.properties
}

bootJar {
    mainClass = 'ai.h2o.mojos.deploy.gcp.vertex.ai.GcpVertexAiApplication'
    archiveClassifier = 'boot'
}

jar {
    enabled = true
}

// Docker image configuration.
jib {
    from {
        if (dockerIncludePython.toBoolean()) {
          image = 'openkbs/jre-mvn-py3:latest'
        } else {
          image = javaBaseImage
        }
    }
    to {
        image = dockerRepositoryPrefix + 'google-vertex-ai-scorer'
        tags = [version]
        auth {
            username = System.getenv('TO_DOCKER_USERNAME') ?: ''
            password = System.getenv('TO_DOCKER_PASSWORD') ?: ''
        }
    }
    container {
        jvmFlags = defaultJibContainerJvmFlags.split(" ").each { it.trim() }
        ports = ['8080']
        environment = [
                // The expected path to the DAI license file.
                DRIVERLESS_AI_LICENSE_FILE: '/tmp/license.sig',
        ]
    }
}

tasks.build.dependsOn tasks.jibBuildTar
